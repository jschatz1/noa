%lex

%%

\/\/.*                            /* skip comment */
\/\*(.|\s)*\*\/                   /* skip comments */

\s+                               /* skip whitespace */

\b(pub|pvt)\b                                                  return 'DEF'
\breturn\b                                                     return 'RETURN'
\b(let|const)\b                                                return 'LET'
\bclass\b                                                      return 'CLASS'
\bextension\b                                                  return 'EXTENSION'
\bstruct\b                                                     return 'STRUCT'
\bextends\b                                                    return 'EXTENDS'
\bif\b                                                         return 'IF'
\belse\b                                                       return 'ELSE'
\btrue\b                                                       return 'TRUE'
\bfalse\b                                                      return 'FALSE'
\bnull\b                                                       return 'NULL'

[+\-]?(?:0|[1-9]\d*)(?:\.\d*)?(?:[eE][+\-]?\d+)?               return 'NUMBER'
\'([^\\\']|\\.)*\'|\"([^\\\"]|\\.)*\"                          return 'STRING'
\w+                                                            return 'IDENTIFIER'

[\=!]\=                                                        return 'EQUALITY_OPERATOR'
[><]\=?                                                        return 'RELATIONAL_OPERATOR'

\&\&                                                           return 'LOGICAL_AND'
\|\|                                                           return 'LOGICAL_OR'

\=                                                             return 'SIMPLE_ASSIGN'
[*/\+\-]\=                                                     return 'COMPLEX_ASSIGN'

[\+\-]                                                         return 'ADDITIVE_OPERATION'
[\*\/]                                                         return 'MULTIPLICATIVE_OPERATION'

/lex

%{

// Define AST node classes.
class Node {}

/* JS comments work here */
class Binary extends Node {}
class Primary extends Node {}

// Can define callbacks for parse events here,
// attaching to `yyparse` object.

yyparse.onParseBegin = (string) => {
};

yyparse.onParseEnd = (value) => {
};

yyparse.onToken = (a,b,c,d) => {
};

yyparse.onShift = (a,b,c,d) => {
};

function BinaryExpression(left, op, right) {
  return {
    type: 'BinaryExpression',
    op,
    left,
    right
  };
}

function Literal(namePrefix, value) {
  console.log("Literal")
  return {
    type: `${namePrefix}Literal`,
    value,
  }
}

function IfStatement(test, consequent, alternate) {
   return {
    type: 'IfStatement',
    test,
    consequent,
    alternate
   }
}

function LogicalExpression(left, op, right) {
  return {
    type: 'LogicalExpression',
    left,
    op,
    right
  }
}

%}

%nonassoc THEN
%nonassoc ELSE
%nonassoc PRIMARY

%%

Program
  : StatementList
    {
      $$ = {
        type: 'Program',
        body: $1,
      }  
    }
  ;

StatementList
  : Statement
    { $$ = [$1] }
  | StatementList Statement
    { $1.push($2); $$ = $1 }
  ;

Statement
  : ExpressionStatement
  | EmptyStatement
  | BlockStatement
  | ClassDeclaration
  | ExtensionDeclaration
  | FunctionDeclaration
  | ReturnStatement
  | IfStatement
  | VariableStatement
  ;

VariableStatement
  : LET VariableDeclarationList
    {
      $$ = {
        type: 'VariableStatement',
        declaration: $2
      }
    }
  ;

VariableDeclarationList
  : VariableDeclaration
    { $$ = [$1] }
  | VariableDeclarationList ',' VariableDeclaration
    { $1.push($3); $$ = $1 }
  ;

VariableDeclaration
  : TypeIdentifier OptVariableInitializer
    {
      $$ = {
        type: 'VariableDeclaration',
        id: $1,
        init: $2,
      }
    }
  ;

OptVariableInitializer
  : VariableInitializer
  | /* empty */
  ;

VariableInitializer
  : SIMPLE_ASSIGN AssignmentExpression
    { $$ = $2 }
  ;

IfStatement
  : IF '(' Expression ')' Statement %prec THEN
    { $$ = IfStatement($3, $5) }
  | IF '(' Expression ')' Statement ELSE Statement 
    { $$ = IfStatement($3, $5, $7) }
  ;

FunctionDeclaration
  : DEF Identifier '(' OptFormalParameterList ')' OptFunctionReturnType BlockStatement
    {
      $$ = {
        type: 'FunctionDeclaration',
        visibility: $1,
        name: $2,
        params: $4,
        returnType: $6,
        body: $7
      }
    }
  ;

ClassDeclaration
  : CLASS Identifier OptClassExtends Statement
    {
      $$ = {
        type: 'ClassDeclaration',
        id: $2,
        superClass: $3,
        body: $4
      }
    }
  ;

ExtensionDeclaration
   : EXTENSION Identifier Statement
    {
      $$ = {
        type: 'ExtensionDeclaration',
        id: $2,
        body: $3
      }
    }
  ;

OptClassExtends 
  : ClassExtends
  | /* empty */
    { $$ = null }
  ;

ClassExtends
  : EXTENDS Identifier
    { $$ = $2 }
  ;

Identifier
  : IDENTIFIER
    {
      $$ = {
        type: 'Identifier',
        value: $1,
      }
    }
  ;

TypeIdentifier
  : Identifier ':' Identifier
    {
      $$ = {
        type: 'TypeIdentifier',
        value: {
          name: $1, type: $3
        }
      } 
    }
  ;

OptFormalParameterList
  : FormalParameterList
  | /* empty */
    { $$ = [] }
  ;

OptFunctionReturnType
  : FormalFunctionReturnType
  | /* empty */
    { $$ = null }
  ;

FormalFunctionReturnType
  : ':' Identifier
  { $$ = { type: $2 } }
  ;

FormalParameterList
  : TypeIdentifier
    { $$ = [$1] }
  | FormalParameterList ',' TypeIdentifier
    { $1.push($3); $1 }
  ;

ExpressionStatement
  : Expression ';'
    { 
      $$ = {
        type: 'ExpressionStatement',
        expression: $1,
      }
    }
  ;

ReturnStatement
  : RETURN OptExpression ';'
    {
      $$ = {
        type: 'ReturnStatement',
        argument: $2,
      }
    }
  ;

OptExpression
  : Expression
  | /* empty */
    { $$ = null }
  ;

EmptyStatement
  : ';'
  {
    $$ = {
      type: 'EmptyStatement',
    }
  }
  ;

BlockStatement
  : '{' OptStatementList '}'
  {
    $$ = {
      type: 'BlockStatement',
      body: $2
    }
  }
  ;

OptStatementList
  : StatementList
  | /* empty */
    { $$ = [] }
  ;

Expression
  : AssignmentExpression
  ;

AssignmentExpression
  : LogicalORExpression
  | LeftHandSideExpression AssignmentOperator AssignmentExpression
    {
      $$ = {
        type: 'AssignmentExpression',
        left: $1,
        op: $2,
        right: $3
      }
    }
  ;

LeftHandSideExpression
  : Identifier
  ;

AssignmentOperator
  : SIMPLE_ASSIGN
  | COMPLEX_ASSIGN
  ;

LogicalORExpression
  : LogicalANDExpression
  | LogicalORExpression LOGICAL_OR LogicalANDExpression
    { $$ = LogicalExpression($1, $2, $3) }
  ;

LogicalANDExpression
  : EqualityExpression
  | LogicalANDExpression LOGICAL_AND EqualityExpression
    { $$ = LogicalExpression($1, $2, $3) }
  ;

EqualityExpression
  : RelationalExpression
  | EqualityExpression EQUALITY_OPERATOR RelationalExpression
    { $$ = BinaryExpression($1, $2, $3) }
  ;

RelationalExpression
  : AdditiveExpression
  | RelationalExpression RELATIONAL_OPERATOR AdditiveExpression
    { $$ = BinaryExpression($1, $2, $3) }
  ;

AdditiveExpression
  : AdditiveExpression ADDITIVE_OPERATION MultiplicativeExpression
    {
      $$ = BinaryExpression($1, $2, $3)
    }
  | MultiplicativeExpression
  ;

MultiplicativeExpression
  : MultiplicativeExpression MULTIPLICATIVE_OPERATION PrimaryExpression
    {
      $$ = BinaryExpression($1, $2, $3)
    }
  | PrimaryExpression
  ;

PrimaryExpression
  : Literal %prec PRIMARY
  | ParenthesizedExpression
  ;

Literal
  : StringLiteral
  | NumericLiteral
  | TrueLiteral
  | FalseLiteral
  | NullLiteral
  ;

TrueLiteral
  : TRUE { $$ = Literal('Boolean', true) }
  ;

FalseLiteral
  : FALSE { $$ = Literal('Boolean', false) }
  ;

NullLiteral
  : NULL { $$ = Literal('Numeric', null) }
  ;

StringLiteral
  : STRING
    {
      $$ = {
        type: 'StringLiteral',
        value: String($1)
      }
    }
  ;

NumericLiteral
  : NUMBER 
    {
      $$ = {
        type: 'NumericLiteral',
        value: Number($1)
      }
    }
  ;

ParenthesizedExpression
  : '(' Expression ')' { $$ = $2 }
  ;